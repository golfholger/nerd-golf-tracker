import groovy.xml.MarkupBuilder

apply plugin: 'idea'

idea.workspace.iws.withXml { doNotShowVcsRootErrors it }
idea.project.ipr.withXml { disableSpellChecking it.asNode() }
idea {
    module {
        testSourceDirs += sourceSets.cucumber.java.srcDirs
        testSourceDirs += sourceSets.cucumber.resources.srcDirs
        scopes.TEST.plus += [configurations.cucumberRuntime]
    }
}

def doNotShowVcsRootErrors (workspaceXmlProvider) {
    def xml = '<option name="SHOW_VCS_ERROR_NOTIFICATIONS" value="false" />'
    def vcsConfig = workspaceXmlProvider.node.component.find { it.@name == 'VcsManagerConfiguration' }
    vcsConfig.append new XmlParser().parseText(xml)
}


def disableSpellChecking (projectRoot) {
    def writer = new StringWriter()
    def xml = new MarkupBuilder(writer)
    def profileName = 'Project Default'
    xml.component(name:'InspectionProjectProfileManager') {
        profiles {
            profile(version:'1.0', is_locked:false) {
                option(name:'myName', value:profileName)
                option(name:'myLocal', value:false)
                inspection_tool(class:'SpellCheckingInspection', enabled:false, level:'TYPO', enabled_by_default:'false')
            }
        }
        option(name:'PROJECT_PROFILE', value:profileName)
        option(name:'USE_PROJECT_PROFILE', value:true)
        version(value:'1.0')
    }

    projectRoot.append new XmlParser().parseText(writer.toString())
}

