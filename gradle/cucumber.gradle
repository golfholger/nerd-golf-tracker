configurations {
    cucumberCompile {
        extendsFrom testImplementation
    }
}

sourceSets {
    cucumber {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}


class Cucumber extends JavaExec {
    def Cucumber() {
        project.tasks.check.dependsOn this
        mustRunAfter project.tasks.test

        setMain 'cucumber.api.cli.Main'
        classpath project.sourceSets.cucumber.runtimeClasspath
        dependsOn project.tasks.compileCucumberJava, project.tasks.jar

        args '--strict',
             '--glue', 'de.itagile.golf',
             '--plugin', 'pretty', 
             'src/cucumber/resources'
    }
}

dependencies {
    testImplementation 'io.cucumber:cucumber-java:4.4.0'
    testImplementation 'io.cucumber:cucumber-junit:4.4.0'
    testImplementation 'io.cucumber:cucumber-picocontainer:4.4.0'
}

task cucumber(type: Cucumber) {
    args '--tags', 'not @in_arbeit'
}

