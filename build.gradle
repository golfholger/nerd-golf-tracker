def apply (String... basenamesWithoutExt) {
    basenamesWithoutExt.each { apply from: "gradle/${it}.gradle" }
}
apply 'dslExtensions'


apply plugin: 'java'
defaultTasks 'clean', 'build'


sourceCompatibility = 1.11
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()
}

archivesBaseName = "nerd-golf-tracker"
jar.manifest {
    attributes('Main-Class': 'de.itagile.golf.Main')
}

sourceSets.test.java { srcDir 'src/unittests/java' }
dependencies {
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:2.+'
}

apply 'cucumber'


apply 'eclipse', 'idea'
